<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.3">
  <POU Name="csp_transfer_infeed_accU_waterfallAdjust" Id="{c61fad8e-0427-44c5-b9c5-75b32615599b}" SpecialFunc="None">
    <Declaration><![CDATA[(*********************************** G.D S.p.A. - Technical Department ***********************************
============= PROGRAM INFO ============
FOLDER:             TBD
MACHINES INVOLVED:  TBD 

============= DESCRIPTION =============
csp machine declaration

============= REVISIONS ===============
Date		Version	 	Author      	Description             
----------	-------		------------  	--------------------------------------------
2019/04/05 	0000.00	 	M.Grasso		Creation of the PRG
2020/05/04 	0000.01	 	G.Imbriaco		From original author, G.Imbriaco: AccU Change motor with absolute encoder and brake.
										If position_req_mm parameter different from actual_pos_mm and machine reset_edge
										the motor move to position_req then disable again
										Manual movement with basc axis object under test( preset, moving,...)
										All disabled if main accU_Change is not Operational

**********************************************************************************************************)
(*$R process_ref := prc; $*)
PROGRAM csp_transfer_infeed_accU_waterfallAdjust
VAR
	
	prc:											process;
	prc_path:										STRING;
	
	axis:											axis;
	Slot84_drv:										coe_drive;

	not_in_position:								message;
	axis_restore_absolute_position_fb:				restore_absolute_position_with_retain_fb;

	disable_req:									BOOL:=FALSE;
	position_req_mm:								LREAL:=0.0;(*$P unit:= mm; $*)
	move_velocity_req_mms:							LREAL:=10.0;(*$P unit:= mm; $*)
	position_req_tolerance_mm:						LREAL:=0.2;	(*$P $*)
	
	axis_status:									axis_status_enum := AXIS_STATUS_PARAMETERS_START;(*$M $*)
	in_position:									BOOL:=FALSE;(*$M $*)	
	actual_position_mm:								LREAL:=0.0; (*$M unit:= mm; $*)
	axis_move_condition:							BOOL:=FALSE;
	torque_limit:                                   INT := 5000;   
    torque_limit_old:                               INT;
	
	fsm_status:										csp_transfer_infeed_accU_waterfallAdjust_enum;(*$M $*)
	fsm_next_status:								csp_transfer_infeed_accU_waterfallAdjust_enum;
	fsm_enable_condition:							BOOL:=FALSE;
	debug_state_machine:							BOOL:=FALSE;
	debug_state_machine_one_step:					BOOL:=FALSE;
	NC_encoder_val AT %I*:							DINT;
	
	axis_direction_FWD: 							BOOL;
	waterfall_move_before_infeed: 					BOOL := FALSE;
	ACCEL_MMpSEC2:									LREAL :=100;
	DECEL_MMpSEC2:									LREAL :=100;
END_VAR
VAR PERSISTENT
	axis_restore_absolute_position_data: 			restore_absolute_position_with_retain_struct;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[_read_var_input();

_object_manager();

IF NOT objects_running()
THEN
	RETURN;	
END_IF

_read_process_image();

_mode_manager();

CASE process_get_mode(prc) OF
	MODE_ABSENT,
	MODE_INACTIVE:
		RETURN;
	MODE_TEST:
		_handle_process_test();
END_CASE

_set_runtime_properties();

(********************************* BODY *********************************)
//Axis Torque Limit
IF torque_limit <> Torque_limit_old
    THEN
    IF coe_drive_write_parameter(
        index:=16#7010 ,
        complete_access:= FALSE ,
        sub_index:= 16#0B,
        data_source:= ADR(torque_limit),
        data_lenght:= SIZEOF(torque_limit),
        CONVERSION_this:=csp_transfer_infeed_accU_waterfallAdjust.axis.coe_drive^ ) = COE_DRIVE_RETURN_VALUE_DONE
        THEN
		torque_limit_old := Torque_limit;
	END_IF
END_IF 

//Axis move condition
IF (
		csp_main_accuChange.transfer_infeed_accU_depth_move_req 
		AND
		waterfall_move_before_infeed
	)
	OR
	(
		csp_main_accuChange.transfer_infeed_accU_waterfall_move_req 
	)
	OR
	(
		axis_move_condition
		AND
		NOT axis_is_disabled(axis)
	)
THEN
	axis_move_condition := TRUE;
ELSE
	axis_move_condition := FALSE;
END_IF

//Sequence of moving motors to avoid interference. If waterfall going up then go before infeed depth. If going down, then infeed depth first.
IF position_req_mm > axis.actual_position
THEN
	axis_direction_FWD := TRUE;
	waterfall_move_before_infeed := TRUE;
ELSE
	axis_direction_FWD := FALSE;
	waterfall_move_before_infeed := FALSE;
END_IF

(*FSM Control*)

fsm_enable_condition := axis_is_ok(FALSE, axis)
						AND
						process_subtree_is_ready(prc)
						AND
							(
								process_get_mode(csp_main_accuChange.prc) = MODE_OPERATIONAL
							)
						AND
						axis_restore_absolute_position_fb.o_position_restored
						AND
						axis_move_condition
						AND
						NOT jog_manager_is_jog_axis_active(SMTR61710_TRANSFER_INFEED_WATERFALL_ADJUST, csp_main_jogManager.jog_manager_accuchange)
						AND
						NOT disable_req;
					
IF	(NOT fsm_enable_condition
	OR
	disable_req)
	AND
	fsm_status <> CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_DISABLED
	AND
	fsm_status < CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_STOP_REQUEST
THEN
	fsm_status	:=	CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_STOP_REQUEST;
END_IF


CASE fsm_status OF
	
	CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_NOT_INITIALIZED:
	
        IF	axis_is_ok(return_when_absent := TRUE, CONVERSION_this := axis)
        THEN
            fsm_next_status := CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_DISABLED;
        END_IF

		
	CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_DISABLED:
	
		IF	fsm_enable_condition
		THEN
			(* Axis with absolute encoder, no home procedure request *)
			fsm_next_status := CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_POSITION_CHECK;
		END_IF;
	
		
	CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_POSITION_CHECK:

		(* CHECKS IF AXIS IS IN TOLERANCE *)
		IF 	NOT in_position
			AND machine_is_not_running(csp.mch)  
		THEN
			(* NOT IN TOLERANCE - NEED TO ENABLE AND MOVE *)
			fsm_next_status := CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_ENABLE_REQUEST;
		END_IF			
	
		
	CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_ENABLE_REQUEST:
	
		axis_enable(axis);
		IF axis_is_standstill(axis)
		THEN
			fsm_next_status	:=	CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_ENABLED;
		END_IF
		

	CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_ENABLED:
	
		IF axis_move_condition 
		THEN
			fsm_next_status	:= CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_ABS_MOVE_REQUEST;	
		END_IF

		
	CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_ABS_MOVE_REQUEST:
	
		IF	axis_is_standstill(axis)
		THEN
			 axis_move_absolute(
				position 		:= position_req_mm,
				velocity 		:= move_velocity_req_mms,
				direction		:= SEL(axis_direction_FWD, AXIS_NEGATIVE_DIRECTION, AXIS_POSITIVE_DIRECTION),
				acceleration 	:= ACCEL_MMpSEC2,//csp_main_accuChange.CONSTANTS.MAX_ACCELERATION_MMpSEC2 * 0.5,
				deceleration 	:= DECEL_MMpSEC2,//csp_main_accuChange.CONSTANTS.MAX_DECELERATION_MMpSEC2 * 0.5,
				jerk 			:= csp_main_accuChange.CONSTANTS.MAX_JERK_MMpSEC3, 
				buffer_mode 	:= AXIS_BUFFER_MODE_BUFFERED,
				CONVERSION_this := axis
				); 
			fsm_next_status	:=	CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_ABS_MOVING;
		ELSE
			fsm_next_status	:=	CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_STOP_REQUEST;	
		END_IF

		
	CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_ABS_MOVING:

		IF axis_is_standstill(axis)
		THEN
			fsm_next_status := CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_STOP_REQUEST;
		END_IF
		
		
	CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_STOP_REQUEST:

		axis_stop(
		    deceleration 	:= csp_main_accuChange.CONSTANTS.MAX_DECELERATION_MMpSEC2 * 0.5,
		    jerk 			:= csp_main_accuChange.CONSTANTS.MAX_JERK_MMpSEC3,
		    CONVERSION_this 			:= axis);

		IF 	axis_is_standstill(axis)
			OR
			NOT axis_is_ok(FALSE, axis)
			OR
			axis_is_disabled(axis)
		THEN
			fsm_next_status := CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_DISABLE_REQUEST;
		END_IF

		
	CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_DISABLE_REQUEST:
	
   		axis_disable(axis);
        IF	axis_is_disabled(axis)
        THEN
            fsm_next_status := CSP_TRANSFER_INFEED_ACCU_WATERFALL_ADJUST_STATUS_DISABLED;
        END_IF;

END_CASE;

IF NOT debug_state_machine
	OR
	debug_state_machine_one_step
THEN
	fsm_status := fsm_next_status;
	debug_state_machine_one_step := FALSE;
END_IF

(************************************************************************)
_brand_change();

_handle_messages();

_write_output();]]></ST>
    </Implementation>
    <Action Name="_brand_change" Id="{2173dec1-8052-44d3-9f76-f833ef519521}">
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Action>
    <Action Name="_handle_messages" Id="{88dc9fc3-fbeb-48e4-a0a7-1ca89df427c9}">
      <Implementation>
        <ST><![CDATA[IF in_position OR csp_main_jogManager.jog_is_active
THEN
	;//process_message_reset(not_in_position);
ELSE
	IF csp_main_accuChange.machine_size_complete = FALSE
	THEN
		;//process_message_reset(not_in_position);
	ELSE
		;//process_message_set(errorID_1:= 1, errorID_2:= 0, errorID_3:= 0, CONVERSION_this:= not_in_position);
	END_IF
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="_handle_process_test" Id="{03e02083-1e7b-4f12-9e5a-9069b171fb20}">
      <Implementation>
        <ST><![CDATA[;]]></ST>
      </Implementation>
    </Action>
    <Action Name="_mode_manager" Id="{441b5017-70c4-4f86-873d-92724db19962}">
      <Implementation>
        <ST><![CDATA[IF process_mode_req_is_pending(prc)
THEN
    process_apply_mode_req_first_step(prc);
    CASE process_get_mode(prc) OF
        MODE_TO_ABSENT:
			;
        MODE_TO_INACTIVE:
			;
        MODE_TO_TEST:
			;
        MODE_TO_OPERATIONAL:
			;
    END_CASE
    process_apply_mode_req_second_step(prc);
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="_object_manager" Id="{6327fe74-3b09-4895-ad82-cd9692a37037}">
      <Implementation>
        <ST><![CDATA[IF objects_initialization()
THEN
	sub_process_init(
		object_name						:= 'csp_transfer_infeed_accU_waterfallAdjust.prc', 
		process							:= csp_transfer_infeed_accU.prc, 
		CONVERSION_this					:= prc);
		
	prc_path 							:= process_get_path(prc);
	
	IF csp_main_configurator.Physical_hw 
	THEN
		axis_init(
			object_name					:= CONCAT(prc_path, '_axis'), 
			axis_type					:= AXIS_TYPE_SERVO, 
			encoder_scaling_factor 		:= 0.1058418*0.00000095367431640625, // 0.1058418mm/rev
			modulo 						:= 1024, 
  			process						:= prc, 
			CONVERSION_this				:= axis);
			
		axis_init_absolute_encoder_preset(	
			position					:=	942.4,
			CONVERSION_this				:=	axis);

		axis_add_safety_integrated_series(
			safety_integrated_series	:= csp_transfer_main_safety_accUChange.sis,
			CONVERSION_this				:= axis);
		axis_set_brake_type(AXIS_BRAKE_DRIVE_CMD, axis);
	ELSE
		axis_init(
			object_name 				:= CONCAT(prc_path, '_axis'),
			axis_type 					:= AXIS_TYPE_VIRTUAL,
			encoder_scaling_factor 		:= G_VIRTUAL_ENCODER_SCALING_FACTOR_DEGPINC,
			modulo 						:= G_MODULO_360_DEG,
			process 					:= prc,
			CONVERSION_this 			:= axis);
	END_IF
		
	axis_set_rotation_direction(
		invert_encoder_counting			:= TRUE,
		invert_motor_polarity			:= TRUE,
		CONVERSION_this					:= axis);
	
	coe_drive_init(
		object_name 					:= CONCAT(prc_path, '_Slot84_drv'),
		device_name 					:= 'IO61600Slot84 (EL7221)',
		vendor 							:= DRIVE_BECKHOFF_EL72x1,
		mode 							:= COE_DRIVE_MODE_DRIVEN_BY_AXIS,
		ethercat_master					:= csp_main.cabinet_transfer_ethercat_master,
		process							:= prc,
		CONVERSION_this 				:= Slot84_drv);

	axis_add_coe_drive(
		coe_drive						:= Slot84_drv,
		CONVERSION_this					:= axis);
	
	axis_set_max_dynamic(
		reference_velocity				:=	csp_main_accuChange.CONSTANTS.MAX_VELOCITY_MMpSEC,
		velocity						:=	csp_main_accuChange.CONSTANTS.MAX_VELOCITY_MMpSEC,
		acceleration					:=	csp_main_accuChange.CONSTANTS.MAX_ACCELERATION_MMpSEC2,
		deceleration					:=	csp_main_accuChange.CONSTANTS.MAX_DECELERATION_MMpSEC2,
		jerk							:=	csp_main_accuChange.CONSTANTS.MAX_JERK_MMpSEC3,
		following_error					:=	csp_main_accuChange.CONSTANTS.MAX_FOLLOWING_ERROR_MM,
		CONVERSION_this					:=	axis);
	
	jog_manager_add_axis(
		list_index						:= SMTR61710_TRANSFER_INFEED_WATERFALL_ADJUST,
		axis							:= axis,
		master_axis						:= FALSE,
		jog_type						:= JOG_TYPE_LINEAR,
		sw_limit_positive				:= 400, //MCP
		sw_limit_negative				:= 0,
		CONVERSION_this					:= csp_main_jogManager.jog_manager_accuchange);

	process_message_init(
		object_name:= CONCAT(prc_path, '_not_in_position'), 
		behaviour:= MESSAGE_BEHAVIOUR_STATE, 
		reset_timeout:= 0, 
		disableable:= TRUE, 
		individual_reset:= FALSE, 
		machine_reaction:= MACHINE_REACTION_STOP_IN_PHASE_ONE, 
		typology:= MESSAGE_TYPOLOGY_ABNORMAL, 
		process:= prc, 
		CONVERSION_this:= not_in_position);
END_IF

IF objects_running()
THEN
	process_cycle(prc);
	axis_cycle(axis);
	coe_drive_cycle(Slot84_drv);
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="_read_process_image" Id="{93db50f7-d842-4374-86f2-22ce87723a9f}">
      <Implementation>
        <ST><![CDATA[axis_restore_absolute_position_fb(
	io_retain_data				:= axis_restore_absolute_position_data, 
	io_axis						:= axis);]]></ST>
      </Implementation>
    </Action>
    <Action Name="_read_var_input" Id="{350b1b7d-c3a6-44f9-ac16-bfdfca159093}">
      <Implementation>
        <ST><![CDATA[;]]></ST>
      </Implementation>
    </Action>
    <Action Name="_set_runtime_properties" Id="{5c414bd6-23cd-4920-98da-2164964a3214}">
      <Implementation>
        <ST><![CDATA[
axis_status := axis_get_status(axis);
actual_position_mm :=axis_get_actual_modulo_position(axis); (* axis_get_actual_modulo_position(axis); only with modulo*)

IF ABS(position_req_mm - actual_position_mm)< position_req_tolerance_mm
	OR
	process_get_mode(prc)= MODE_ABSENT
	OR
	process_get_mode(csp_main_accuChange.prc) = MODE_ABSENT
THEN
	in_position := TRUE;
ELSE
	in_position := FALSE;	
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="_write_output" Id="{7d3ac5fc-d3a1-466a-844b-46156b3647bb}">
      <Implementation>
        <ST><![CDATA[;]]></ST>
      </Implementation>
    </Action>
  </POU>
</TcPlcObject>