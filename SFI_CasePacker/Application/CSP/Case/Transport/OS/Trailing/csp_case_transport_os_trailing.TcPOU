<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.3">
  <POU Name="csp_case_transport_os_trailing" Id="{c3787f7e-c3d8-4dc5-ad19-33fb9847bf93}" SpecialFunc="None">
    <Declaration><![CDATA[(*********************************** G.D S.p.A. - Technical Department ***********************************
============= PROGRAM INFO ============
FOLDER:             TBD
MACHINES INVOLVED:  TBD 

============= DESCRIPTION =============
csp machine declaration

============= REVISIONS ===============
Date		Version	 	Author      	Description             
----------	-------		------------  	--------------------------------------------
2019/04/05 	0000.00	 	M.Grasso		Creation of the PRG

**********************************************************************************************************)
(*$R process_ref := prc; $*)
PROGRAM csp_case_transport_os_trailing
VAR
	
	prc:												process;
	prc_path:											STRING;
	
	fsm_enable_condition:								BOOL := FALSE;
	fsm_next_status:									CSP_CASE_TRANSPORT_STATUS_enum;
	fsm_status:											CSP_CASE_TRANSPORT_STATUS_enum;
	
	axis:												axis;
	phase_velocity_degps:								LREAL := 20.0;
	axis_calculate_cam_slave_master_position_deg:		LREAL := 0.0;
	axis_calculated_cam_slave_position_deg:				LREAL := 0.0;
	slave_position_has_been_calculated:					BOOL  := FALSE;
	phase_enable:										BOOL:=TRUE;
	engage_enable:										BOOL:=TRUE;
	disable_req:										BOOL:=FALSE;
	debug_state_machine:								BOOL:=FALSE;
	debug_state_machine_one_step:						BOOL:=FALSE;
	cam_id:												DINT  := CASE_TRANSPORT_RUN_CAM;
	slave_scaling:										LREAL := 1; (*$P $*)
	master_scaling:										LREAL := 1; (*$P $*)
	master_offset:										LREAL := 0;
	slave_offset: 										LREAL := 0;
	
	axis_is_ready_to_enabled_dvr:						BOOL := FALSE;
	axis_is_ok_dvr:										BOOL := FALSE;
	axis_is_enabled_dvr:								BOOL := FALSE;
	axis_is_disabled_dvr:								BOOL := FALSE;
	axis_is_standstill_dvr:								BOOL := FALSE;
	axis_is_homing_dvr:									BOOL := FALSE;
	axis_is_home_done_dvr:								BOOL := FALSE;
	axis_is_cam_enagaged_dvr:							BOOL := FALSE;
	soe_drive_is_ok_dvr:								BOOL := FALSE;
	safety_is_ok_dvr:									bool;
	
	
	axis_position_nmtr:									LREAL;
	
	
	torque_limit:										INT := 2000;    (*$P $*)
	torque_limit_old:									INT;	
	
	home_position_master_offset_deg: 					LREAL := 0;
	
	in_accu_safe_pos:                                   BOOL := FALSE;
    accu_tolerance:                                     INT := 5;
    accu_safe_pos:                                      INT;	(*$P $*)
	
	servo_on_condition: 								BOOL;
	axis_tolerance:										INT := 30;
	phase_direction: 									BOOL;
	enable_delay: 										TON;
	Torque_force_constant: 								LREAL := 0.98;
END_VAR

VAR CONSTANT
	MOTOR_MAX_VELOCITY_DEGPS:							LREAL := 9600;
	MOTOR_MAX_ACCELLERATION_DEGPS2:						LREAL := 4800;
	MOTOR_MAX_DECELLERATION_DEGPS2:						LREAL := 4800;
	MOTOR_MAX_JERK_DEGPS3:								LREAL := 100000000; 
	MOTOR_MAX_FOLLOWING_ERROR_DEG:						LREAL := 2;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[_read_var_input();

_object_manager();

IF NOT objects_running()
THEN
	RETURN;	
END_IF

_read_process_image();

_mode_manager();

CASE process_get_mode(prc) OF
	MODE_ABSENT:
		IF axis_get_mode(axis) = MODE_TEST
		THEN
			safety_integrated_series_close(csp_case_main_safety_drives_SD2100.chB_smes_sis);
		END_IF
		RETURN;
	MODE_INACTIVE:
		RETURN;
	MODE_TEST:
		_handle_process_test();
END_CASE

_set_runtime_properties();

(********************************* BODY *********************************)
axis.actual_torque := (axis.actual_current * Torque_force_constant)/1000;
(*FSM Control*)

IF ABS(axis.actual_position - accu_safe_pos)  < accu_tolerance
	OR (360 - axis.actual_position) < accu_tolerance
THEN
	in_accu_safe_pos := TRUE;
ELSE
	in_accu_safe_pos := FALSE;   
END_IF				
				
fsm_enable_condition := axis_is_ok(FALSE, axis)
						AND
						process_is_ready(prc)
						AND
						process_subtree_is_ready(prc)
						AND						
						(csp_main_motorization.motorization_axis_redefined_at_powerup OR csp_main_accuchange.Brand_Change_Occurred)
						AND
						servo_on_condition
						AND
						NOT jog_manager_is_jog_axis_active(SMTR2123_CASE_TRANSPORT_OS_TRAILING, csp_main_jogManager.jog_manager_real)
						AND
						NOT disable_req;

IF	NOT fsm_enable_condition
	AND
	fsm_status <> CSP_CASE_TRANSPORT_STATUS_DISABLED
	AND
	fsm_status < CSP_CASE_TRANSPORT_STATUS_STOP_REQUEST
THEN
	fsm_status	:=	CSP_CASE_TRANSPORT_STATUS_DISABLE_REQUEST;
END_IF;

IF torque_limit <> Torque_limit_old
THEN
	 IF soe_drive_channel_write_parameter(
		channel:=csp_case_transport_os_trailing.axis.soe_drive_channel, 
		idn:= S_0_IDN + 92, 
		data_element:=soe_drive_element_value , 
		data_source:= ADR(torque_limit), 
		data_lenght:= SIZEOF(torque_limit), 
		CONVERSION_this:= csp_case_transport_os_trailing.axis.soe_drive^)= SOE_DRIVE_RETURN_VALUE_DONE 
	THEN
		torque_limit_old := Torque_limit;
	END_IF	
END_IF

CASE fsm_status OF

	CSP_CASE_TRANSPORT_STATUS_NOT_INITIALIZED:
	
        IF	axis_is_ok(return_when_absent := TRUE, CONVERSION_this := axis)
        THEN
            fsm_next_status := CSP_CASE_TRANSPORT_STATUS_DISABLED;
        END_IF
		

	CSP_CASE_TRANSPORT_STATUS_DISABLED:

		 IF fsm_enable_condition
			AND
			(
			(motorization_manager_is_starting(csp_main_motorization_manager.motm))
			OR
			jog_manager_is_jog_axis_active(JOG_LIST_CSP_CASE_TRANSPORT_VIRTUAL, csp_main_jogManager.jog_manager_virtual)
			OR
			jog_manager_is_jog_axis_active(JOG_LIST_CSP_CASE_VIRTUAL, csp_main_jogManager.jog_manager_virtual)	
			)                              
		THEN
			debug_state_machine := FALSE;
			fsm_next_status := CSP_CASE_TRANSPORT_STATUS_ENABLE_REQUEST;
		END_IF;

		
	CSP_CASE_TRANSPORT_STATUS_ENABLE_REQUEST:
		
		IF NOT csp_main_accuChange.machine_size_complete
		THEN
			fsm_next_status	:= CSP_CASE_TRANSPORT_STATUS_CHANGE_OVER_MODE;
		ELSE	
			axis_enable(axis);
			IF axis_is_standstill(axis)
			THEN
				fsm_next_status	:=	CSP_CASE_TRANSPORT_STATUS_ENABLED;
			END_IF;		
			
		END_IF;
		
		
	CSP_CASE_TRANSPORT_STATUS_CHANGE_OVER_MODE:
		
		IF csp_main_accuChange.Motion_Motors_New_Size_Req
		THEN
			axis_enable(axis);
			IF axis_is_standstill(axis)
			THEN
				fsm_next_status	:=	CSP_CASE_TRANSPORT_STATUS_ENABLED;
			END_IF;	
		END_IF;


	CSP_CASE_TRANSPORT_STATUS_ENABLED:

		fsm_next_status	:=	CSP_CASE_TRANSPORT_STATUS_HOME_REQUEST;

		
	CSP_CASE_TRANSPORT_STATUS_HOME_REQUEST: 

		fsm_next_status	:=	CSP_CASE_TRANSPORT_STATUS_HOME_DONE;

		
	CSP_CASE_TRANSPORT_STATUS_HOMING:

		axis.home.done := TRUE;

		IF axis_home_done(axis)
		THEN
			fsm_next_status	:=	CSP_CASE_TRANSPORT_STATUS_HOME_DONE;
		END_IF

		
	CSP_CASE_TRANSPORT_STATUS_HOME_DONE:
	
		IF phase_enable
		THEN
			fsm_next_status	:= CSP_CASE_TRANSPORT_STATUS_CALCULATE_SLAVE_POSITION_REQUEST;
		END_IF
		
		
	CSP_CASE_TRANSPORT_STATUS_CALCULATE_SLAVE_POSITION_REQUEST:
	
		(*Calculate a desired master axis position.
		Function used  LMOD: Ex. LMOD(400.56, 360) = 40.56
		home_position_master_offset_deg is helpful for changing the 1st term of LMOD*)
		axis_calculate_cam_slave_master_position_deg := 
		LMOD	(axis_get_actual_modulo_position(csp_case_transport_virtualMaster.axis) (*master axis*)
							+
				home_position_master_offset_deg, (*initialized at 0° position*)
					G_MODULO_360_DEG											);
		(*master_scaling and slave_scaling initialized at 360° since the cams are normed in a range equal to 0-1.*)
			
		IF axis_calculate_cam_slave_dynamics_data(
				cam_id := cam_id,
				master_scaling := master_scaling,
				master_position := axis_calculate_cam_slave_master_position_deg,
				slave_scaling := slave_scaling,
				CONVERSION_this := axis)
		THEN
			fsm_next_status := CSP_CASE_TRANSPORT_STATUS_CALCULATING_SLAVE_POSITION;
		END_IF

		
	CSP_CASE_TRANSPORT_STATUS_CALCULATING_SLAVE_POSITION:
	
		IF	axis_cam_slave_dynamics_data_calculated(axis)
		THEN
			(*The “axis_get_cam_slave_dynamics_position” method returns the position of the slave within the cam plate table at the master position calculated for the instance specified: *)

			axis_calculated_cam_slave_position_deg :=(axis_get_cam_slave_dynamics_position(axis) + slave_offset);	
			slave_position_has_been_calculated := TRUE;
			phase_direction := FALSE;
			fsm_next_status := CSP_CASE_TRANSPORT_STATUS_PHASE_REQUEST;
		END_IF

	
	CSP_CASE_TRANSPORT_STATUS_PHASE_REQUEST:
	
		IF  LMOD((axis_calculated_cam_slave_position_deg + axis_tolerance+G_MODULO_360_DEG),G_MODULO_360_DEG)> LMOD((axis.actual_position + 360),G_MODULO_360_DEG) 
			OR
			LMOD((axis_calculated_cam_slave_position_deg - axis_tolerance+G_MODULO_360_DEG),G_MODULO_360_DEG)< LMOD((axis.actual_position + 360),G_MODULO_360_DEG) 
		
		THEN 
				phase_direction := FALSE;	// shortest path
		ELSE
				phase_direction := TRUE; //forward
		END_IF
	
		IF	axis_is_standstill(axis)
		THEN
			 axis_move_absolute(
				position 		:= axis_calculated_cam_slave_position_deg,(*calculated above, the slave is moved*)
				velocity 		:= phase_velocity_degps,
				direction		:= AXIS_SHORTEST_WAY, //SEL(phase_direction, AXIS_SHORTEST_WAY, AXIS_POSITIVE_DIRECTION),
				acceleration 	:= MOTOR_MAX_ACCELLERATION_DEGPS2,
				deceleration 	:= MOTOR_MAX_DECELLERATION_DEGPS2,
				jerk 			:= MOTOR_MAX_JERK_DEGPS3, 
				buffer_mode 	:= AXIS_BUFFER_MODE_BUFFERED,
				CONVERSION_this 			:= axis
				); 
		END_IF
		
		IF axis_is_absolute_moving(axis)
		THEN
			fsm_next_status	:=	CSP_CASE_TRANSPORT_STATUS_PHASING;
		END_IF

		
	CSP_CASE_TRANSPORT_STATUS_PHASING:

		IF axis_is_standstill(axis)
		THEN
			fsm_next_status	:=	CSP_CASE_TRANSPORT_STATUS_PHASED;
		END_IF

		
	CSP_CASE_TRANSPORT_STATUS_PHASED:

		IF engage_enable
		THEN
			fsm_next_status := CSP_CASE_TRANSPORT_STATUS_CAM_ENGAGED_REQ;
		END_IF


	CSP_CASE_TRANSPORT_STATUS_CAM_ENGAGED_REQ:
		
		(* WAITS FOR MOVE REQUEST FOR CROSSPUSHER AND CAMS TO CROSSPUSHER VIRTUAL MASTER *)
		axis_engage_cam(
			cam_id 			:= cam_id,
			master_engage_mode 	:= AXIS_CAM_ENGAGE_ABSOLUTE,
			slave_engage_mode 	:= AXIS_CAM_ENGAGE_ABSOLUTE, 
			activation_mode 	:= AXIS_CAM_ACTIVATION_IMMEDIATELY, 
			activation_position	:= 0,
			master_scaling_mode	:= AXIS_CAM_SCALING_MODE_USER_DEFINED,
			slave_scaling_mode	:= AXIS_CAM_SCALING_MODE_USER_DEFINED,
			master_offset 		:= master_offset,	
			slave_offset 		:= slave_offset,
			master_scaling 		:= master_scaling,
			slave_scaling 		:= slave_scaling,
			interpolation_type 		:= AXIS_CAM_INTERPOLATION_SYSTEM_DEFAULT,
			master 			:= csp_case_transport_virtualMaster.axis,
			CONVERSION_this 	:= axis);	
			
		IF	axis_is_cam_engaged(axis)
		THEN
			fsm_next_status := CSP_CASE_TRANSPORT_STATUS_CAM_ENGAGED;
		END_IF

	
	CSP_CASE_TRANSPORT_STATUS_AXIS_ENGAGE_VERIFY :
	
		;
		
		
	CSP_CASE_TRANSPORT_STATUS_CAM_ENGAGED:
	
		IF	machine_is_stop_emergency_active(csp.mch)
			AND motorization_manager_is_stopped(csp_main_motorization_manager.motm)
		THEN
			fsm_next_status := CSP_CASE_TRANSPORT_STATUS_DISENGAGE_REQUEST;
		END_IF
		
		
	CSP_CASE_TRANSPORT_STATUS_RUNNING:

		;
		
		
	CSP_CASE_TRANSPORT_STATUS_DISENGAGE_REQUEST:

		fsm_next_status := CSP_CASE_TRANSPORT_STATUS_DISENGAGING;
		
		
	CSP_CASE_TRANSPORT_STATUS_DISENGAGING:
	
		IF axis_disengage(
			mode:= AXIS_DISENGAGE_AND_ON_PHASE_STOP, 
			position:= 0, 
			velocity:= 0, 
			acceleration:= MOTOR_MAX_ACCELLERATION_DEGPS2, 
			deceleration:= MOTOR_MAX_DECELLERATION_DEGPS2, 
			jerk:= MOTOR_MAX_JERK_DEGPS3, 
			CONVERSION_this:= axis)
		THEN
			fsm_next_status := CSP_CASE_TRANSPORT_STATUS_CAM_ENGAGED_REQ;
		END_IF
	
		
	CSP_CASE_TRANSPORT_STATUS_STOP_REQUEST:

		axis_stop(
		    deceleration 	:= MOTOR_MAX_DECELLERATION_DEGPS2,
		    jerk 			:= MOTOR_MAX_JERK_DEGPS3,
		    CONVERSION_this 			:= axis);

		IF 	axis_is_standstill(axis)
			OR
			NOT axis_is_ok(FALSE, axis)
			OR
			axis_is_disabled(axis)
		THEN
			fsm_next_status := CSP_CASE_TRANSPORT_STATUS_DISABLE_REQUEST;
		END_IF

		
	CSP_CASE_TRANSPORT_STATUS_DISABLE_REQUEST:
	
   		axis_disable(axis);
   		fsm_next_status := CSP_CASE_TRANSPORT_STATUS_DISABLING;

		
	CSP_CASE_TRANSPORT_STATUS_DISABLING:
	
        IF	axis_is_disabled(axis)
        THEN
            fsm_next_status := CSP_CASE_TRANSPORT_STATUS_DISABLED;
        END_IF

END_CASE;

IF NOT debug_state_machine
	OR
	debug_state_machine_one_step
THEN
	fsm_status := fsm_next_status;
	debug_state_machine_one_step := FALSE;
END_IF

(************************************************************************)

_handle_messages();

_write_output();]]></ST>
    </Implementation>
    <Action Name="_handle_messages" Id="{f9db04b5-e731-4339-a0f7-d4bb285cff12}">
      <Implementation>
        <ST><![CDATA[;]]></ST>
      </Implementation>
    </Action>
    <Action Name="_handle_process_test" Id="{c3aa8144-2776-4404-b42e-9be191505814}">
      <Implementation>
        <ST><![CDATA[;]]></ST>
      </Implementation>
    </Action>
    <Action Name="_mode_manager" Id="{8cf3d35d-ae96-43f2-9577-13b9f3cb088d}">
      <Implementation>
        <ST><![CDATA[IF process_mode_req_is_pending(prc)
THEN
    process_apply_mode_req_first_step(prc);
    CASE process_get_mode(prc) OF
        MODE_TO_ABSENT:
			;
        MODE_TO_INACTIVE:
			;
        MODE_TO_TEST:
			;
        MODE_TO_OPERATIONAL:
			;
    END_CASE
    process_apply_mode_req_second_step(prc);
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="_object_manager" Id="{a02b2e99-691e-4ad2-ba86-23e35381fe38}">
      <Implementation>
        <ST><![CDATA[IF objects_initialization()
THEN
	sub_process_init(
		object_name								:= 'csp_case_transport_os_trailing.prc', 
		process									:= csp_case_transport_os.prc, 
		CONVERSION_this							:= prc);
		
	prc_path 									:= process_get_path(prc);
	
		(* axis smes *)
	IF csp_main_configurator.Physical_hw
		THEN
		
			axis_init(
				object_name						:= CONCAT(prc_path, '_axis'), 
				axis_type						:= AXIS_TYPE_SERVO, 
				encoder_scaling_factor			:= G_SERVO_ENCODER_SCALING_FACTOR_DEGPINC,
				modulo							:= G_MODULO_360_DEG, 
				process							:= prc, 
				CONVERSION_this					:= axis);
				
			axis_add_safety_integrated_series(
				safety_integrated_series 		:= csp_case_main_safety_drives_SD2100.chB_smes_sis,
				CONVERSION_this					:= axis);
				
			axis_init_absolute_encoder_preset(
        		position                        := 0.0,
        		CONVERSION_this                 := axis);	
				
				
		ELSE
			axis_init(
				object_name						:= CONCAT(prc_path, '_axis'), 
				axis_type						:= AXIS_TYPE_VIRTUAL, 
				encoder_scaling_factor			:= G_VIRTUAL_ENCODER_SCALING_FACTOR_DEGPINC,
				modulo							:= G_MODULO_360_DEG, 
				process							:= prc, 
				CONVERSION_this					:= axis);		
				
	END_IF
	
	axis_set_max_dynamic(
		reference_velocity						:=	MOTOR_MAX_VELOCITY_DEGPS,
		velocity								:=	MOTOR_MAX_VELOCITY_DEGPS,
		acceleration							:=	MOTOR_MAX_ACCELLERATION_DEGPS2,
		deceleration							:=	MOTOR_MAX_DECELLERATION_DEGPS2,
		jerk									:=	MOTOR_MAX_JERK_DEGPS3,
		following_error							:=	MOTOR_MAX_FOLLOWING_ERROR_DEG,
		CONVERSION_this							:=	axis);
		
		
	jog_manager_add_axis(
		list_index								:= SMTR2123_CASE_TRANSPORT_OS_TRAILING,
		axis									:= axis,
		master_axis								:= FALSE,
		jog_type								:= JOG_TYPE_LINEAR,
		sw_limit_positive						:= 800,
		sw_limit_negative						:= 0,
		CONVERSION_this							:= csp_main_jogManager.jog_manager_real);
	
	axis_add_soe_drive(
		channel									:=	SOE_DRIVE_CHANNEL_B,
		soe_drive								:=	csp_case_main_drives.SD2100_drv,
		CONVERSION_this							:=	axis);

END_IF

IF objects_running()
THEN
	process_cycle(prc);
	axis_cycle(axis); 
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="_read_process_image" Id="{67bae478-ad3b-4b98-bd76-358b9549e2a5}">
      <Implementation>
        <ST><![CDATA[	axis_is_ready_to_enabled_dvr:= axis_is_ready_to_enable(axis);
	axis_is_ok_dvr:=	 axis_is_ok(FALSE, axis);
	axis_is_enabled_dvr:= axis_is_enabled(axis);
	axis_is_disabled_dvr:= axis_is_disabled(axis);
	axis_is_standstill_dvr:= axis_is_standstill(axis);
	axis_is_homing_dvr:= axis_is_homing(axis);
	axis_is_home_done_dvr:= axis_home_done(axis);
	axis_is_cam_enagaged_dvr:= axis_is_cam_engaged(axis);
	soe_drive_is_ok_dvr:= soe_drive_is_ok(FALSE, csp_case_main_drives.SD2100_drv);
	safety_is_ok_dvr := csp_case_main_safety.series_closed_dvr;
	
	axis_position_nmtr:= axis_get_actual_modulo_position(axis);]]></ST>
      </Implementation>
    </Action>
    <Action Name="_read_var_input" Id="{242c3e81-a7e7-40cd-8e5a-de0039ee6828}">
      <Implementation>
        <ST><![CDATA[disable_req := csp_case.disable_req;]]></ST>
      </Implementation>
    </Action>
    <Action Name="_set_runtime_properties" Id="{8a57016d-7648-4615-8c5b-4a65d600c496}">
      <Implementation>
        <ST><![CDATA[enable_delay(IN:=axis_is_ready_to_enable(axis) , PT:=T#2S , Q=> servo_on_condition , ET=> );]]></ST>
      </Implementation>
    </Action>
    <Action Name="_write_output" Id="{9e944bc9-1ce9-4b98-95f6-4fa8ff276715}">
      <Implementation>
        <ST><![CDATA[;]]></ST>
      </Implementation>
    </Action>
  </POU>
</TcPlcObject>