<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.3">
  <POU Name="csp_case_glue_ls_accU_upperPosition" Id="{26470755-b587-4fdd-a214-8fcf27675c44}" SpecialFunc="None">
    <Declaration><![CDATA[(*********************************** G.D S.p.A. - Technical Department ***********************************
============= PROGRAM INFO ============
FOLDER:             TBD
MACHINES INVOLVED:  TBD 

============= DESCRIPTION =============
csp machine declaration

============= REVISIONS ===============
Date		Version	 	Author      	Description             
----------	-------		------------  	--------------------------------------------
2019/04/05 	0000.00	 	M.Grasso		Creation of the PRG
2020/05/06 	0000.01	 	G.Imbriaco		From original author, G.Imbriaco: AccU Change motor with absolute encoder and brake.
										If position_req_mm parameter different from actual_pos_mm and machine reset_edge
										the motor move to position_req then disable again
										Manual movement with basc axis object under test( preset, moving,...)
										All disabled if main accU_Change is not Operational

**********************************************************************************************************)
(*$R process_ref := prc; $*)
PROGRAM csp_case_glue_ls_accU_upperPosition
VAR
	
	prc:											process;
	prc_path:										STRING;
	
	axis:											axis;
	Slot44_drv:										coe_drive;

	not_in_position:								message;
	axis_restore_absolute_position_fb:				restore_absolute_position_with_retain_fb;

	disable_req:									BOOL:=FALSE;
	position_req:									LREAL:=0.0;(*$P $*)
	move_velocity_req:								LREAL:=10.0;(*$P $*)
	position_req_tolerance:							LREAL:=0.2;	(*$P $*)
	
	axis_status:									axis_status_enum := AXIS_STATUS_PARAMETERS_START;(*$M $*)
	in_position:									BOOL:=FALSE;(*$M $*)	
	actual_position:								LREAL:=0.0; (*$M $*)
	axis_move_condition:							BOOL:=FALSE;
	torque_limit:                                   INT := 2000;    (*$P $*)
    torque_limit_old:                               INT;
	
	fsm_status:										csp_case_glue_ls_accU_upperPosition_enum;(*$M $*)
	fsm_next_status:								csp_case_glue_ls_accU_upperPosition_enum;
	fsm_enable_condition:							BOOL:=FALSE;
	debug_state_machine:							BOOL:=FALSE;
	debug_state_machine_one_step:					BOOL:=FALSE;
	NC_encoder_val AT %I*:							DINT;
	
END_VAR
VAR PERSISTENT
	axis_restore_absolute_position_data:			restore_absolute_position_with_retain_struct;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[_read_var_input();

_object_manager();

IF NOT objects_running()
THEN
	RETURN;	
END_IF

_read_process_image();

_mode_manager();

CASE process_get_mode(prc) OF
	MODE_ABSENT,
	MODE_INACTIVE:
		RETURN;
	MODE_TEST:
		_handle_process_test();
END_CASE

_set_runtime_properties();

(********************************* BODY *********************************)
//Axis Torque Limit
IF torque_limit <> Torque_limit_old
THEN
    IF coe_drive_write_parameter(
        index:=16#7010 ,
        complete_access:= FALSE ,
        sub_index:= 16#0B,
        data_source:= ADR(torque_limit),
        data_lenght:= SIZEOF(torque_limit),
        CONVERSION_this:=csp_case_glue_ls_accU_upperPosition.axis.coe_drive^ ) = COE_DRIVE_RETURN_VALUE_DONE
     THEN
		torque_limit_old := Torque_limit;
     END_IF
END_IF

//Axis move condition
IF csp_main_accuChange.case_glue_ls_accU_upperPosition_move_req 
THEN
	axis_move_condition := TRUE;
ELSE
	axis_move_condition := FALSE;
END_IF

(*FSM Control*)
fsm_enable_condition := axis_is_ok(FALSE, axis)
						(*AND
						process_is_ready(prc) *)
						AND
						process_subtree_is_ready(prc)
						AND
						process_get_mode(csp_main_accuChange.prc) = MODE_OPERATIONAL
						AND
						axis_restore_absolute_position_fb.o_position_restored
						AND
						axis_move_condition
						AND
						NOT jog_manager_is_jog_axis_active(SMTR61400_CASE_GLUE_LS_UPPER_POSITION, csp_main_jogManager.jog_manager_accuchange)
						AND
						NOT disable_req;	
					
IF	(NOT fsm_enable_condition
	OR
	disable_req)
	AND
	fsm_status <> CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_DISABLED
	AND
	fsm_status < CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_STOP_REQUEST
THEN
	fsm_status	:=	CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_STOP_REQUEST;
END_IF

CASE fsm_status OF
	
	CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_NOT_INITIALIZED:
	
        IF	axis_is_ok(return_when_absent := TRUE, CONVERSION_this := axis)
        THEN
            fsm_next_status := CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_DISABLED;
        END_IF

		
	CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_DISABLED:
	
		IF	fsm_enable_condition
		THEN
			(* Axis with absolute encoder, no home procedure request *)
			fsm_next_status := CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_POSITION_CHECK;
		END_IF;
	
		
	CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_POSITION_CHECK:

		(* CHECKS IF AXIS IS IN TOLERANCE *)
		IF NOT in_position
			AND machine_is_not_running(csp.mch)  
		THEN
			(* NOT IN TOLERANCE - NEED TO ENABLE AND MOVE *)
			fsm_next_status := CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_ENABLE_REQUEST;
		ELSE
			(* ALREADY IN POSITION SO GO TO DISABLED *)
			fsm_next_status := CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_DISABLED;
		END_IF				
	
		
	CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_ENABLE_REQUEST:
	
		axis_enable(axis);
		IF axis_is_standstill(axis)
		THEN
			fsm_next_status	:=	CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_ENABLED;
		END_IF		

		
	CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_ENABLED:
	
		IF axis_move_condition THEN
			fsm_next_status	:= CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_ABS_MOVE_REQUEST;
		END_IF	

		
	CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_ABS_MOVE_REQUEST:

		IF	axis_is_standstill(axis)
		THEN
			 axis_move_absolute(
				position 		:= position_req,
				velocity 		:= move_velocity_req,
				direction		:= AXIS_SHORTEST_WAY,
				acceleration 	:= csp_main_accuChange.CONSTANTS.MAX_ACCELERATION_MMpSEC2,
				deceleration 	:= csp_main_accuChange.CONSTANTS.MAX_DECELERATION_MMpSEC2,
				jerk 			:= csp_main_accuChange.CONSTANTS.MAX_JERK_MMpSEC3, 
				buffer_mode 	:= AXIS_BUFFER_MODE_BUFFERED,
				CONVERSION_this := axis
				); 
			fsm_next_status	:=	CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_ABS_MOVING;
		ELSE
			fsm_next_status	:=	CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_STOP_REQUEST;	
		END_IF
		

	CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_ABS_MOVING:

		IF in_position //TRUNC(axis.actual_position) = TRUNC(position_req)
		THEN
			fsm_next_status := CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_STOP_REQUEST;
		END_IF
		
		
	CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_STOP_REQUEST:

		axis_stop(
		    deceleration 	:= csp_main_accuChange.CONSTANTS.MAX_DECELERATION_MMpSEC2,
		    jerk 			:= csp_main_accuChange.CONSTANTS.MAX_JERK_MMpSEC3,
		    CONVERSION_this 			:= axis);

		IF 	axis_is_standstill(axis)
			OR
			NOT axis_is_ok(FALSE, axis)
			OR
			axis_is_disabled(axis)
		THEN
			fsm_next_status := CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_DISABLE_REQUEST;
		END_IF

		
	CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_DISABLE_REQUEST:
   		axis_disable(axis);
        IF	axis_is_disabled(axis)
        THEN
            fsm_next_status := CSP_CASE_GLUE_LS_ACCU_UPPERPOSITION_STATUS_DISABLED;
        END_IF;

END_CASE;

IF NOT debug_state_machine
	OR
	debug_state_machine_one_step
THEN
	fsm_status := fsm_next_status;
	debug_state_machine_one_step := FALSE;
END_IF

(************************************************************************)
_brand_change();

_handle_messages();

_write_output();]]></ST>
    </Implementation>
    <Action Name="_brand_change" Id="{703cf455-8933-4698-9bd6-f73a00f7b088}">
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Action>
    <Action Name="_handle_messages" Id="{abb17c8b-3c7e-4662-ade3-6a04dc876174}">
      <Implementation>
        <ST><![CDATA[IF in_position OR csp_main_jogManager.jog_is_active
THEN
	;//process_message_reset(not_in_position);
ELSE
	IF csp_main_accuChange.machine_size_complete = FALSE
	THEN
		;//process_message_reset(not_in_position);
	ELSE
		;//process_message_set(errorID_1:= 1, errorID_2:= 0, errorID_3:= 0, CONVERSION_this:= not_in_position);
	END_IF
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="_handle_process_test" Id="{47b55c0c-98fd-4e03-932a-58f8647d4c22}">
      <Implementation>
        <ST><![CDATA[;]]></ST>
      </Implementation>
    </Action>
    <Action Name="_mode_manager" Id="{3c1bb8a8-679a-421b-9252-fa8b4f1843b7}">
      <Implementation>
        <ST><![CDATA[IF process_mode_req_is_pending(prc)
THEN
    process_apply_mode_req_first_step(prc);
    CASE process_get_mode(prc) OF
        MODE_TO_ABSENT:
			;
        MODE_TO_INACTIVE:
			;
        MODE_TO_TEST:
			;
        MODE_TO_OPERATIONAL:
			;
    END_CASE
    process_apply_mode_req_second_step(prc);
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="_object_manager" Id="{a1b4dc63-91bf-40bd-873d-8f9d36c0b53f}">
      <Implementation>
        <ST><![CDATA[IF objects_initialization()
THEN
	sub_process_init(
		object_name						:= 'csp_case_glue_ls_accU_upperPosition.prc', 
		process							:= csp_case_glue_ls_accU.prc, 
		CONVERSION_this					:= prc);
		
	prc_path 							:= process_get_path(prc);
	
	IF csp_main_configurator.Physical_hw 
	THEN
		axis_init(
			object_name					:= CONCAT(prc_path, '_axis'), 
			axis_type					:= AXIS_TYPE_SERVO, 
			encoder_scaling_factor		:= 29.6*0.00000095367431640625,
 			modulo						:= G_MODULO_4320_DEG, 
  			process						:= prc, 
			CONVERSION_this				:= axis);

		axis_add_safety_integrated_series(
			safety_integrated_series	:= csp_case_main_safety_accUChange.sis,
			CONVERSION_this				:= axis);
			
		axis_init_absolute_encoder_preset(
        	position                     := 1024,
        	CONVERSION_this              := axis);
		axis_set_brake_type(AXIS_BRAKE_DRIVE_CMD, axis);
	ELSE
		axis_init(
			object_name 				:= CONCAT(prc_path, '_axis'),
			axis_type 					:= AXIS_TYPE_VIRTUAL,
			encoder_scaling_factor 		:= G_VIRTUAL_ENCODER_SCALING_FACTOR_DEGPINC,
			modulo 						:= 0,
			process 					:= prc,
			CONVERSION_this 			:= axis);
	END_IF
		
	axis_set_rotation_direction(
		invert_encoder_counting			:= TRUE,
		invert_motor_polarity			:= TRUE,
		CONVERSION_this					:= axis);

	coe_drive_init(
		object_name 					:= CONCAT(prc_path, '_Slot44_drv'),
		device_name 					:= 'IO6100Slot44 (EL7211)',
		vendor 							:= DRIVE_BECKHOFF_EL72x1,
		mode 							:= COE_DRIVE_MODE_DRIVEN_BY_AXIS,
		ethercat_master					:= csp_main.cabinet_ethercat_master,
		process							:= prc,
		CONVERSION_this 				:= Slot44_drv);

	axis_add_coe_drive(
		coe_drive						:= Slot44_drv,
		CONVERSION_this					:= axis);
	
	axis_set_max_dynamic(
		reference_velocity				:=	csp_main_accuChange.CONSTANTS.MAX_VELOCITY_MMpSEC,
		velocity						:=	csp_main_accuChange.CONSTANTS.MAX_VELOCITY_MMpSEC,
		acceleration					:=	csp_main_accuChange.CONSTANTS.MAX_ACCELERATION_MMpSEC2,
		deceleration					:=	csp_main_accuChange.CONSTANTS.MAX_DECELERATION_MMpSEC2,
		jerk							:=	csp_main_accuChange.CONSTANTS.MAX_JERK_MMpSEC3,
		following_error					:=	csp_main_accuChange.CONSTANTS.MAX_FOLLOWING_ERROR_MM,
		CONVERSION_this					:=	axis);
	
	jog_manager_add_axis(
		list_index						:= SMTR61400_CASE_GLUE_LS_UPPER_POSITION,
		axis							:= axis,
		master_axis						:= FALSE,
		jog_type						:= JOG_TYPE_LINEAR,
		sw_limit_positive				:= 30,
		sw_limit_negative				:= 0,
		CONVERSION_this					:= csp_main_jogManager.jog_manager_accuchange);
		
	process_message_init(
		object_name:= CONCAT(prc_path, '_not_in_position'), 
		behaviour:= MESSAGE_BEHAVIOUR_STATE, 
		reset_timeout:= 0, 
		disableable:= TRUE, 
		individual_reset:= FALSE, 
		machine_reaction:= MACHINE_REACTION_NO_STOP, 
		typology:=  MESSAGE_TYPOLOGY_NOT_VISIBLE, 
		process:= prc, 
		CONVERSION_this:= not_in_position);
		
END_IF

IF objects_running()
THEN
	process_cycle(prc);
	axis_cycle(axis);
	coe_drive_cycle(Slot44_drv);
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="_read_process_image" Id="{18385b90-bd97-4f49-a6d6-d322c4bcb20c}">
      <Implementation>
        <ST><![CDATA[axis_restore_absolute_position_fb(
	io_retain_data				:= axis_restore_absolute_position_data, 
	io_axis						:= axis);]]></ST>
      </Implementation>
    </Action>
    <Action Name="_read_var_input" Id="{00a18b58-f06d-40bd-ace5-d7d3e91cb1e4}">
      <Implementation>
        <ST><![CDATA[;]]></ST>
      </Implementation>
    </Action>
    <Action Name="_set_runtime_properties" Id="{3627d6e6-3f49-496c-a757-536a22b9ee05}">
      <Implementation>
        <ST><![CDATA[
axis_status := axis_get_status(axis);
actual_position :=axis_get_actual_modulo_position(axis); (* axis_get_actual_modulo_position(axis); only with modulo*)

IF ABS(position_req - actual_position)< position_req_tolerance
	OR
	process_get_mode(prc)= MODE_ABSENT
	OR
	process_get_mode(csp_main_accuChange.prc) = MODE_ABSENT
THEN
	in_position := TRUE;
ELSE
	in_position := FALSE;
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="_write_output" Id="{85579c3b-efcf-4d94-b8ea-568971ffe80d}">
      <Implementation>
        <ST><![CDATA[;]]></ST>
      </Implementation>
    </Action>
  </POU>
</TcPlcObject>